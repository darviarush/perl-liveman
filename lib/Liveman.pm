package Liveman;
use 5.008001;
use strict;
use warnings;
use utf8;

our $VERSION = "0.01";

use Term::ANSIColor qw/colored/;
use File::Slurper qw/read_text write_text/;
use Markdown::To::POD qw/markdown_to_pod/;


# ÐšÐ¾Ð½ÑÑ‚Ñ€ÑƒÐºÑ‚Ð¾Ñ€
sub new {
    my $cls = shift;
    my $self = bless {@_}, $cls;
    delete $self->{files} if $self->{files} && !@{$self->{files}};
    $self
}

# ÐŸÐ¾Ð»ÑƒÑ‡Ð¸Ñ‚ÑŒ Ð¿ÑƒÑ‚ÑŒ Ðº Ñ‚ÐµÑÑ‚Ð¾Ð²Ð¾Ð¼Ñƒ Ñ„Ð°Ð¹Ð»Ñƒ Ð¸Ð· Ð¿ÑƒÑ‚Ð¸ Ðº md-Ñ„Ð°Ð¹Ð»Ñƒ
sub test_path {
    my ($self, $md) = @_;
    $md =~ s!^lib/(.*?)([^/]*)\.md$!"t/$1" . (lcfirst($2) =~ s/[A-Z]/"-".lc $&/gre) . ".t" !e;
    $md
}

# Ð¢Ñ€Ð°Ð½ÑÑ„Ð¾Ñ€Ð¼Ð¸Ñ€ÑƒÐµÑ‚ md-Ñ„Ð°Ð¹Ð»Ñ‹
sub transforms {
    my ($self) = @_;
    my $mds = $self->{files} // [split /\n/, `find lib -name '*.md'`];

    $self->{count} = 0;

    for my $md (@$mds) {
        my $test = $self->test_path($md);
        my $mdmtime = (stat $md)[9];
        die "ÐÐµÑ‚ Ñ„Ð°Ð¹Ð»Ð° $md" if !$mdmtime;
        $self->transform($md, $test) if !-e $test || -e $test && $mdmtime > (stat $test)[9];
    }
    $self
}

# Ð­ÑÐºÐµÐ¹Ð¿Ð¸Ð½Ð³ Ð´Ð»Ñ ÑÑ‚Ñ€Ð¾ÐºÐ¸ Ð² Ð´Ð²Ð¾Ð¹Ð½Ñ‹Ñ… ÐºÐ°Ð²Ñ‹Ñ‡ÐºÐ°Ñ…
sub _qq_esc {
    $_[0] =~ s!"!\\"!gr
}

# Ð­ÑÐºÐµÐ¹Ð¿Ð¸Ð½Ð³ Ð´Ð»Ñ ÑÑ‚Ñ€Ð¾ÐºÐ¸ Ð² Ð¾Ð´Ð¸Ð½Ð°Ñ€Ð½Ñ‹Ñ… ÐºÐ°Ð²Ñ‹Ñ‡ÐºÐ°Ñ…
sub _q_esc {
    $_[0] =~ s!'!\\'!gr
}

# ÐžÐ±Ñ€ÐµÐ·Ð°ÐµÑ‚ Ð¿Ñ€Ð¾Ð±ÐµÐ»ÑŒÐ½Ñ‹Ðµ ÑÐ¸Ð¼Ð²Ð¾Ð»Ñ‹
sub _trim {
    $_[0] =~ s!^\s*(.*?)\s*\z!$1!sr
}

# Ð¡Ð¾Ð·Ð´Ð°Ñ‘Ñ‚ Ð¿ÑƒÑ‚ÑŒ
sub _mkpath {
    my ($p) = @_;
    mkdir $`, 0755 while $p =~ /\//g;
}

# Ð¢Ñ€Ð°Ð½ÑÑ„Ð¾Ñ€Ð¼Ð¸Ñ€ÑƒÐµÑ‚ md-Ñ„Ð°Ð¹Ð» Ð² Ñ‚ÐµÑÑ‚ Ð¸ Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ð°Ñ†Ð¸ÑŽ
sub transform {
    my ($self, $md, $test) = @_;
    $test //= $self->test_path($md);

    print "ðŸ”– $md ", colored("â†¦", "white"), " $test ", colored("...", "white"), " ";

    open my $f, "<:utf8", $md or die "$md: $!";
    _mkpath($test);
    open my $t, ">:utf8", $test or die "$test: $!";

    print $t "use strict; use warnings; use utf8; use open qw/:std :utf8/; use Test::More 0.98; ";

    my @text;
    my @markdown;
    my $close_subtest; my $title = 'Start'; my $use_title = 1;
    my $in_code; my $lang;

    while(<$f>) {
        push @text, $_;

        if($in_code) {
            if(/^```/) { # Ð—Ð°ÐºÑ€Ñ‹Ð²Ð°ÐµÐ¼ ÐºÐ¾Ð´
                $in_code = 0;
                print $t "\n";

                pop @text;
                push @markdown, "\n", (map "\t$_", @text), "\n";
                @text = ();
            }
            elsif($lang =~ /^(perl|)$/) {
                if(/#\s*((?<is_deeply>-->|âŸ¶)|(?<is>->|â†’)|(?<qqis>=>|â‡’)|(?<qis>\\>|â†¦))\s*(?<expected>.+?)\s*$/n) {
                    my ($code, $expected) = ($`, $+{expected});
                    my $q = do { _q_esc($_ =~ s!\s*$!!r) }; # Ð¢ÑƒÑ‚ do, Ñ‡Ñ‚Ð¾Ð±Ñ‹ ÑÐ¾Ñ…Ñ€Ð°Ð½Ð¸Ñ‚ÑŒ %+
                    $code = _trim($code);

                    print $t "\t"; # ÐÐ°Ñ‡Ð¸Ð½Ð°ÐµÐ¼ ÑÑ‚Ñ€Ð¾ÐºÑƒ Ñ Ñ‚Ð°Ð±ÑƒÐ»ÑÑ†Ð¸Ð¸

                    if(exists $+{is_deeply}) { print $t "is_deeply scalar do {$code}, scalar do {$expected}, '$q';\n" }
                    elsif(exists $+{is})   { print $t "is scalar do {$code}, scalar do{$expected}, '$q';\n" }
                    elsif(exists $+{qqis}) { my $ex = _qq_esc($expected); print $t "is scalar do {$code}, \"$ex\", '$q';\n" }
                    elsif(exists $+{qis})  { my $ex = _q_esc($expected);  print $t "is scalar do {$code}, '$ex', '$q';\n" }
                    else { # Ð§Ñ‚Ð¾-Ñ‚Ð¾ ÑƒÐ¶Ð°ÑÐ½Ð¾Ðµ Ð²Ñ‹Ñ€Ð²Ð°Ð»Ð¾ÑÑŒ Ð½Ð° Ð²Ð¾Ð»ÑŽ!
                        print $t "???\n";
                    }
                }
                else { # ÐžÐ±Ñ‹Ñ‡Ð½Ð°Ñ ÑÑ‚Ñ€Ð¾ÐºÐ° ÐºÐ¾Ð´Ð°
                    print $t "\t$_";
                }
            }
            else { # ÐÐ° ÐºÐ°ÐºÐ¾Ð¼-Ñ‚Ð¾ Ð´Ñ€ÑƒÐ³Ð¾Ð¼ ÑÐ·Ñ‹ÐºÐµ
                print $t "# $_";
            }
        } else { # Ð’ Ñ‚ÐµÐºÑÑ‚Ðµ

            if(/^(#+)\s*/) { # Ð¡Ð¾Ñ…Ñ€Ð°Ð½ÑÐµÐ¼ Ð·Ð°Ð³Ð¾Ð»Ð¾Ð²Ð¾Ðº
                $title = _trim($');
                print $t "# $_";
            }
            elsif(/^```(\w*)/) { # ÐžÑ‚ÐºÑ€Ñ‹Ð²Ð°ÐµÐ¼ ÐºÐ¾Ð´

                $in_code = 1;
                $lang = $1;
                print $t "\n";

                if($use_title ne $title) {

                    print $t "done_testing; }; " if $close_subtest;
                    $close_subtest = 1;

                    my $title_q = _q_esc($title);
                    print $t "subtest '$title_q' => sub { ";

                    $use_title = $title;
                }

                pop @text;
                push @markdown, markdown_to_pod(join "", @text);
                @text = ();
            }
            else { # Ð”Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚Ð°Ñ†Ð¸ÑŽ Ð¿ÐµÑ‡Ð°Ñ‚Ð°ÐµÐ¼ Ð² Ð²Ð¸Ð´Ðµ ÐºÐ¾Ð¼Ð¼ÐµÐ½Ñ‚Ð°Ñ€Ð¸ÐµÐ², Ñ‡Ñ‚Ð¾Ð±Ñ‹ ÑÐ¾Ñ…Ñ€Ð°Ð½Ð¸Ñ‚ÑŒ Ð½ÑƒÐ¼ÐµÑ€Ð°Ñ†Ð¸ÑŽ ÑÑ‚Ñ€Ð¾Ðº
                print $t "# $_";
            }
        }
    }

    print $t "\n\tdone_testing;\n};\n" if $close_subtest;
    print $t "\ndone_testing;\n";

    close $f;
    close $t;

    print colored("ok", "bright_green"), "\n";

    my $pm = $md =~ s/\.md$/.pm/r;
    if(!-e $pm) {
        my $pkg = ($pm =~ s!^lib/(.*)\.pm$!$1!r) =~ s!/!::!gr;
        write_text $pm, "package $pkg;\n\n1;";
    }

    push @markdown, markdown_to_pod(join "", @text);
    my $pod = join "", @markdown;
 
    my $module = read_text $pm;
    $module =~ s!(^__END__[\t ]*\n.*)?\z!
__END__

=encoding utf-8

$pod!smn;
    write_text $pm, $module;

    $self->{count}++;

    $self
}

# Ð—Ð°Ð¿ÑƒÑÑ‚Ð¸Ñ‚ÑŒ Ñ‚ÐµÑÑ‚Ñ‹
sub tests {
    my ($self) = @_;

    if($self->{files}) {
        local $, = " ";
        $self->{exitcode} = system "yath test -j4 @{$self->{files}}";
        return $self;
    }

    system "cover -delete";
    $self->{exitcode} = system "yath test -j4 --cover" and return $self;
    system "cover -report html_basic";
    system "opera cover_db/coverage.html || xdg-open cover_db/coverage.html" if $self->{open};
    return $self;
}

1;
__END__

=encoding utf-8

=head1 NAME

Liveman - markdown compiller to test and pod.

=head1 SYNOPSIS

    use Liveman;

    my $liveman = Liveman->new;

    # compile lib/Example.md file to t/example.t and added pod to lib/Example.pm
    $liveman->transform("lib/Example.md");

    # compile all lib/**.md files
    $liveman->transforms;

    # start tests with yath
    $liveman->tests;

    # limit liveman to these files for operations transforms and tests (without cover)
    my $liveman = Liveman->new(files => ["lib/Example1.md", "lib/Examples/Example2.md"]);

=head1 DESCRIPTION

The problem with modern projects is that the documentation is disconnected from testing.
This means that the examples in the documentation may not work, and the documentation itself may lag behind the code.

Liveman compile lib/**.md files to t/**.t files
and it added pod-documentation to section __END__ to lib/**.pm files.

Use C<liveman> command for compile the documentation to the tests in catalog of your project and starts the tests:

    liveman
	
=head1 EXAMPLE

Is files:

lib/ray_test_Mod.pm:

	package ray_test_Mod;

	our $A = 10;
	our $B = [1, 2, 3];
	our $C = "\$hi";

	1;

lib/ray_test_Mod.md:
	
	# NAME

	ray_test_Mod â€” Ñ‚ÐµÑÑ‚Ð¾Ð²Ñ‹Ð¹ Ð¼Ð¾Ð´ÑƒÐ»ÑŒ

	# SYNOPSIS

	```perl
	use ray_test_Mod;

	$ray_test_Mod::A # -> 5+5
	$ray_test_Mod::B # --> [1, 2, 3]

	my $dollar = '$';
	$ray_test_Mod::C # => ${dollar}hi

	$ray_test_Mod::C # \> $hi


	$ray_test_Mod::A # â†’ 5+5
	$ray_test_Mod::B # âŸ¶ [1, 2, 3]
	$ray_test_Mod::C # â‡’ ${dollar}hi
	$ray_test_Mod::C # â†¦ $hi

Start C<liveman>:

	liveman -o
	
This command modify C<pm>-file:

lib/ray_test_Mod.pm:

	package ray_test_Mod;

	our $A = 10;
	our $B = [1, 2, 3];
	our $C = "\$hi";

	1;

	__END__

	=encoding utf-8

	=head1 NAME

	ray_test_Mod â€” Ñ‚ÐµÑÑ‚Ð¾Ð²Ñ‹Ð¹ Ð¼Ð¾Ð´ÑƒÐ»ÑŒ

	=head1 SYNOPSIS

		use ray_test_Mod;
		
		$ray_test_Mod::A # -> 5+5
		$ray_test_Mod::B # --> [1, 2, 3]
		
		my $dollar = '$';
		$ray_test_Mod::C # => ${dollar}hi
		
		$ray_test_Mod::C # \> $hi
		
		
		$ray_test_Mod::A # â†’ 5+5
		$ray_test_Mod::B # âŸ¶ [1, 2, 3]
		$ray_test_Mod::C # â‡’ ${dollar}hi
		$ray_test_Mod::C # â†¦ $hi

	
And this command make test:

t/ray_test_-mod.t:

	use strict; use warnings; use utf8; use open qw/:std :utf8/; use Test::More 0.98; # # NAME
	# 
	# ray_test_Mod â€” Ñ‚ÐµÑÑ‚Ð¾Ð²Ñ‹Ð¹ Ð¼Ð¾Ð´ÑƒÐ»ÑŒ
	# 
	# # SYNOPSIS
	# 

	subtest 'SYNOPSIS' => sub { 	use ray_test_Mod;
		
		is scalar do {$ray_test_Mod::A}, scalar do{5+5}, '$ray_test_Mod::A # -> 5+5';
		is_deeply scalar do {$ray_test_Mod::B}, scalar do {[1, 2, 3]}, '$ray_test_Mod::B # --> [1, 2, 3]';
		
		my $dollar = '$';
		is scalar do {$ray_test_Mod::C}, "${dollar}hi", '$ray_test_Mod::C # => ${dollar}hi';
		
		is scalar do {$ray_test_Mod::C}, '$hi', '$ray_test_Mod::C # \> $hi';
		
		
		is scalar do {$ray_test_Mod::A}, scalar do{5+5}, '$ray_test_Mod::A # â†’ 5+5';
		is_deeply scalar do {$ray_test_Mod::B}, scalar do {[1, 2, 3]}, '$ray_test_Mod::B # âŸ¶ [1, 2, 3]';
		is scalar do {$ray_test_Mod::C}, "${dollar}hi", '$ray_test_Mod::C # â‡’ ${dollar}hi';
		is scalar do {$ray_test_Mod::C}, '$hi', '$ray_test_Mod::C # â†¦ $hi';

	# 
	# # DESCRIPTION
	# 
	# It's fine.
	# 
	# # LICENSE
	# 
	# Â© Yaroslav O. Kosmina
	# 2023

		done_testing;
	};

	done_testing;

Run it with coverage.

Option C<-o> open coverage in browser (coverage file: cover_db/coverage.html).

=head1 LICENSE

Copyright (C) Yaroslav O. Kosmina.

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 AUTHOR

Yaroslav O. Kosmina E<lt>darviarush@mail.ruE<gt>

=cut

